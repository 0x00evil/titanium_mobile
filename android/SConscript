import os, sys, platform, glob, shutil
Import("env cwd version")

#TODO enable map
modules = ['api', 'app', 'database', 'filesystem', 'geolocation', 'media', 'ui']

print "Building Titanium Mobile (Android) %s..." % version

if platform.platform() == 'Windows':
	default_android_sdk = 'C:\\android-sdk'
else:
	default_android_sdk = '/opt/android-sdk'

android_sdk = ARGUMENTS.get("android_sdk", default_android_sdk)

classpath = [ os.path.join(cwd, 'android', 'lib', 'js.jar'),
	os.path.join(android_sdk, 'platforms', 'android-1.6', 'android.jar'),
	os.path.join(cwd, 'android', 'lib', 'commons-codec-1.3.jar'),
	os.path.join(cwd, 'android', 'lib', 'commons-logging-1.1.1.jar')]

sources = [os.path.join('src', 'com'), os.path.join('src', 'org'), 'thirdparty']
sources += glob.glob(os.path.join('src', 'ti', 'modules', 'titanium', '*.java'))

env.Append(JAVASOURCEPATH='src')
env.Append(JAVACLASSPATH=classpath)

classes = env.Java(target = 'classes', source = sources)
env.Jar(target = 'titanium.jar', source = classes)

for module in modules:
	module_sources = os.path.join('src', 'ti', 'modules', 'titanium', module)
	env.Java(target = 'classes', source = module_sources, JAVACLASSPATH = classpath + [os.path.join(cwd, 'dist', 'android', 'classes')])
	env.Jar(target = 'titanium-%s.jar' % module,
		source = os.path.join('classes', 'ti', 'modules', 'titanium', module), JARCHDIR=os.path.join(cwd, 'dist', 'android', 'classes'))

